# test some ligatures and fractions close to U0000. Avoid high values such as
# UFxxx since the Unicode chart may add new unicode values later.

Print("Get Table Array Totals.")
lt = ucLigChartGetCnt()
vt = ucVulChartGetCnt()
ot = ucOFracChartGetCnt()
ft = ucFracChartGetCnt()
if ( lt < 50 || lt > 2000 || vt < 10 || vt > 2000 || ot < 10 || vt > 2000)
  Error("Expected totals approximately closer to 500, 50, 20.")
endif
if ( vt + ot != ft )
  Error("Expected totals to add up.")
endif

Print("Test valid (and invalid) ligatures.")
l1 = ucLigChartGetLoc(306)
l2 = ucLigChartGetLoc(0x133)
l3 = ucLigChartGetLoc(0x153)
l4 = ucLigChartGetLoc(0x4b5)
l5 = ucLigChartGetLoc(0x135)
if ( l1 != 0 || l2 != 1 || l3 != 3 || l4 != 7 || l5 != -1 )
  Error("Expected Ligature table array 'n' values of 0,1,3,7,-1.")
endif

Print("Return some ligatures from built-in table.")
l1 = ucLigChartGetNxt(0)
l2 = ucLigChartGetNxt(1)
l3 = ucLigChartGetNxt(3)
l4 = ucLigChartGetNxt(7)
l5 = ucLigChartGetNxt(lt) # table starts at 0, not 1 (last value is at [lt-1])
if ( l1 != 306 || l2 != 307 || l3 != 339 || l4 != 1205 || l5 != -1 )
  Error("Expected Ligatures values from table of: 306,307,339,1205,-1.")
endif

Print("Test valid (and invalid) vulgar fractions.")
v1 = ucVulChartGetLoc(0xbd)
v2 = ucVulChartGetLoc(0x132)
if ( v1 != 1 || v2 != -1 )
  Error("Expected Vulgar Fraction table array 'n' values of: 1,-1.")
endif

Print("Verify uint32 code works.")
o1 = ucOFracChartGetNxt(ot-1)
if ( o1 <= 69000 )
  Error("Expected last Other Fraction of 0x10e7e or larger.")
endif

Print("Test boolean found==1, not_found==0.")
b1 = IsLigature(306)
b2 = IsLigature(0x135)
b3 = IsVulgarFraction(0xbd)
b4 = IsVulgarFraction(0x132)
b5 = IsFraction(0xbd)
b6 = IsFraction(0x132)
if ( b1 != 1 || b2 != 0 || b3 != 1 || b4 != 0 || b5 != 1 || b6 != 0 )
  Error("Expected boolean values of 1,0,1,0,1,0.")
endif

Print("All Tests done and valid.")
